use ir::*;

grammar;

pub Input: Input = {
    Comment* <universal_regions:UniversalRegions> <placeholder_origins:PlaceholderOrigins?> <known_subsets:KnownSubsets?> <var_uses_region:VarUsesRegion?> <var_drops_region:VarDropsRegion?> Comment* <blocks:BlockDefn*> => Input::new(<>)
};

Comment: () = {
    r"//.*"
};

VarRegionMappings = Comma<VarRegionMapping>;
VarRegionMapping: (String, String) = {
                 "(" <Variable> "," <Origin> ")" => (<>),
};

VarUsesRegion = "var_uses_region" "{" <VarRegionMappings> "}";
VarDropsRegion = "var_drops_region" "{" <VarRegionMappings> "}";

UniversalRegions = "universal_regions" "{" <Comma<Origin>> "}";

PlaceholderOrigins = "placeholder_origins" "{" <Comma<PlaceholderOrigin>> "}";
PlaceholderOrigin : PlaceholderOrigin = {
    <origin:Origin> ":" <loan:Loan> => PlaceholderOrigin { <> },
};

KnownSubsets = "known_subsets" "{" <Comma<KnownSubset>> "}";
KnownSubset : KnownSubset = {
    <a:Origin> ":" <b:Origin> => KnownSubset { <> },
};

BlockDefn : Block = {
    "block" <name:Block> "{" <statements:Statement*> Comment* <goto:Goto> "}" => Block { <> },
};

Goto: Vec<String> = {
    "goto" <Comma<Block>> ";",
    () => Vec::new(),
};

Statement : Statement = {
    Comment* <Effects> ";" => Statement::new(<>),
    Comment* <start_effects:Effects> "/" <effects:Effects> ";" => Statement::with_start_effects(<>),
};

Effects = Comma<Effect>;
Effect = {
    <Fact> => Effect::Fact(<>),
    Use
};

Fact : Fact = {
  "outlives" "(" <a:Origin> ":" <b:Origin> ")" => Fact::Outlives { <> },
  "borrow_region_at" "(" <origin:Origin> "," <loan:Loan> ")" => Fact::BorrowRegionAt { <> },
  "invalidates" "(" <loan:Loan> ")" => Fact::Invalidates { <> },
  "kill" "(" <loan:Loan> ")" => Fact::Kill { <> },
  "var_used" "(" <variable:Variable> ")" => Fact::UseVariable { <> },
  "var_defined" "(" <variable:Variable> ")" => Fact::DefineVariable { <> },
  "region_live_at" "(" <origin:Origin> ")" => Fact::RegionLiveAt { <> },
  "var_drop_used" "(" <variable:Variable> ")" => Fact::UseVariable { <> },
};

Use : Effect = "use" "(" <origins:Comma<Origin>> ")" => Effect::Use { <> };

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Origin: String = {
    r"'\w+" => <>.to_string()
};

Block: String = {
    r"B\w+" => <>.to_string()
};

Loan: String = {
    r"L\w+" => <>.to_string()
};

Variable: String = {
    r"V\w+" => <>.to_string()
};

